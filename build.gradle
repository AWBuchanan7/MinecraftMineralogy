buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven-publish"
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
	mavenLocal()
    mavenCentral()
}
 
dependencies {
    testCompile('org.junit.jupiter:junit-jupiter-api:5.0.2')
    testCompile('org.mockito:mockito-core:2.12.0')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.2')
}

junitPlatform {
    platformVersion '1.0.2' // optional, defaults to plugin version
    //logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    // enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}

javadoc {
    failOnError = false
}

def mod_file = getModFile()
version = getVersion("VERSION", mod_file)
if (System.getenv().BUILD_NUMBER) {
    version += "." + System.getenv().BUILD_NUMBER
}
group= "cyano.mineralogy" 
archivesBaseName = "Mineralogy_1.12"
sourceCompatibility = targetCompatibility = "1.8"

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ""
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file "secret.json"
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = "1.12.2-14.23.0.2552"
    runDir = "run"
    mappings = "snapshot_20171030"
//    coreMod = ""
}

repositories {
    maven {
        name "DVS1 Maven FS"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "Chickenbones Repo"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "tterrag"
        url "http://maven.tterrag.com/"
    }
    maven {
        name "MMD"
        url "https://maven.mcmoddev.com/"
    }
    maven {
        name "amadornes"
        url "http://maven.amadornes.com/"
    }
    maven {
        name "epoxide"
        url "http://maven.epoxide.xyz"
    }
}

dependencies {
    // none
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
    }
}

task apiJar(type: Jar) {
    from apiClasses
    classifier = 'apiClasses'
}

task deobfJar(type: Jar) {
    classifier = "deobf"
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.getDestinationDir()
}

artifacts {
//  archives apiJar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact apiJar {
                classifier "api"
            }
            artifact deobfJar {
                classifier "dev"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = ""
    project {
        id = "240974"
        changelog = file("CHANGELOG.txt")
        releaseType = "release"
        addGameVersion("1.12.2")
        def projName = "Minecraft Mineralogy"
        def displayVersion = getVersion("VERSION", mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += "." + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$projName $displayVersion"
        }
//        addArtifact(apiJar) {
//            displayName = "$projName $displayVersion API"
//        }
        addArtifact(sourceJar) {
            displayName = "$projName $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$projName $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$projName $displayVersion Javadoc"
        }
    }
}

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: "src/main/java")
    tree.include "**/*.java"
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
